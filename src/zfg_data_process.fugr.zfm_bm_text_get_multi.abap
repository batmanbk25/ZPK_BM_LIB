FUNCTION ZFM_BM_TEXT_GET_MULTI.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IT_NAME) TYPE  ZTT_TDOBNAME
*"     REFERENCE(I_ID) TYPE  THEAD-TDID DEFAULT '*'
*"     REFERENCE(I_OBJECT) TYPE  THEAD-TDOBJECT
*"  EXPORTING
*"     REFERENCE(ET_TX) TYPE  ZTT_BM_TX
*"----------------------------------------------------------------------
  DATA:
    LT_NAME_RANGE TYPE TSPSRNAME,
    LS_NAME_RANGE TYPE STXDRNAME,
    LT_TEXT_TAB   TYPE TEXT_LH,
    LT_ERR_TAB    TYPE TEXT_LH,
    LS_TX      TYPE ZST_BM_TX,
    BEGIN OF LS_LINE,
      TDLINE TYPE TDLINE,
    END OF LS_LINE,
    LT_LINE LIKE TABLE OF LS_LINE.

  CLEAR: ET_TX.

  CHECK IT_NAME IS NOT INITIAL.
  LOOP AT IT_NAME INTO DATA(LS_NAMET).
    LS_NAME_RANGE-SIGN    = 'I'.
    LS_NAME_RANGE-OPTION  = 'EQ'.
    LS_NAME_RANGE-LOW     = LS_NAMET-NAME.
    APPEND LS_NAME_RANGE TO LT_NAME_RANGE.
  ENDLOOP.

  CALL FUNCTION 'READ_MULTIPLE_TEXTS'
    EXPORTING
      OBJECT                  = I_OBJECT
      ID                      = I_ID
      LANGUAGE                = SY-LANGU
      NAME_RANGES             = LT_NAME_RANGE
    IMPORTING
      TEXT_TABLE              = LT_TEXT_TAB
      ERROR_TABLE             = LT_ERR_TAB
    EXCEPTIONS
      WRONG_ACCESS_TO_ARCHIVE = 1
      OTHERS                  = 2.

  LOOP AT LT_TEXT_TAB INTO DATA(LS_TEXT).
    READ TABLE IT_NAME INTO LS_NAMET
      WITH KEY NAME = LS_TEXT-HEADER-TDNAME.
    IF SY-SUBRC IS INITIAL.
      LS_TX-NAME    = LS_NAMET-NAME.
      LS_TX-TDID    = LS_TEXT-HEADER-TDID.
      MOVE-CORRESPONDING LS_TEXT-LINES TO LT_LINE.
      CONCATENATE LINES OF LT_LINE INTO LS_TX-TEXT
        SEPARATED BY SPACE.
      IF LS_TX-TEXT CA '&'.
        REPLACE ALL OCCURRENCES OF '<(>' IN LS_TX-TEXT WITH SPACE.
        REPLACE ALL OCCURRENCES OF '<)>' IN LS_TX-TEXT WITH SPACE.
      ENDIF.
      LS_TX-LINES = LS_TEXT-LINES.
      APPEND LS_TX TO ET_TX.
    ENDIF.
  ENDLOOP.


ENDFUNCTION.
