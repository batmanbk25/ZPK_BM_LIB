FUNCTION ZFM_BM_DATA2XML_FILE.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IT_TABNAME) TYPE  DDTABNAMES OPTIONAL
*"     REFERENCE(IT_DATA_RAW) TYPE  ZTT_BM_DATA_RAW OPTIONAL
*"     REFERENCE(I_OPENFILE) TYPE  XMARK OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_XML) TYPE  STRING
*"----------------------------------------------------------------------

  DATA:
    LT_XML_STR TYPE TABLE OF STRING,
    LW_XML_STR TYPE STRING,
    BEGIN OF LS_TAB_DATA,
      TABNM  TYPE TABNAME,
      XMLSTR TYPE STRING,
    END OF LS_TAB_DATA,
    LR_DATA     TYPE REF TO DATA,
    LT_TAB_DATA LIKE TABLE OF LS_TAB_DATA.
  DATA:
    LW_FILENAME    TYPE STRING,
    LW_FILEPATH    TYPE STRING,
    LW_FILESIZE    TYPE I,
    LW_PATH        TYPE STRING,
    LW_USER_ACTION TYPE I.

  LOOP AT IT_TABNAME INTO DATA(LS_TABNAME).
    CLEAR: LS_TAB_DATA, LR_DATA.

    LS_TAB_DATA-TABNM = LS_TABNAME-TABNAME.

    CREATE DATA LR_DATA TYPE TABLE OF (LS_TABNAME-TABNAME).
    SELECT *
      FROM (LS_TABNAME-TABNAME)
      INTO TABLE @LR_DATA->*.
    IF SY-SUBRC IS INITIAL.
      CALL TRANSFORMATION ID
        SOURCE DATA = LR_DATA->*
        RESULT XML LS_TAB_DATA-XMLSTR.
    ENDIF.
    APPEND LS_TAB_DATA TO LT_TAB_DATA.
  ENDLOOP.

  LOOP AT IT_DATA_RAW INTO DATA(LS_DATA_RAW).
    CLEAR: LS_TAB_DATA, LR_DATA.

    LS_TAB_DATA-TABNM = LS_DATA_RAW-TABNM.
    CALL TRANSFORMATION ID
      SOURCE DATA = LS_DATA_RAW-REFDATA->*
      RESULT XML LS_TAB_DATA-XMLSTR.
    APPEND LS_TAB_DATA TO LT_TAB_DATA.
  ENDLOOP.

  CALL TRANSFORMATION ID
    SOURCE DATA = LT_TAB_DATA
    RESULT XML E_XML.
  APPEND E_XML TO LT_XML_STR.

* Popup save file
  CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_SAVE_DIALOG
    EXPORTING
      DEFAULT_FILE_NAME = 'DataXML.xml'
    CHANGING
      FILENAME          = LW_FILENAME
      PATH              = LW_PATH
      FULLPATH          = LW_FILEPATH
      USER_ACTION       = LW_USER_ACTION
    EXCEPTIONS
      OTHERS            = 1.
  IF SY-SUBRC <> 0.
    MESSAGE A017(ZMS_COL_LIB).
  ENDIF.
* Process when user choose 1 file
  CHECK LW_USER_ACTION = CL_GUI_FRONTEND_SERVICES=>ACTION_OK.

  CALL FUNCTION 'GUI_DOWNLOAD'
    EXPORTING
      FILENAME = LW_FILENAME
*     CONFIRM_OVERWRITE = 'X'
    TABLES
      DATA_TAB = LT_XML_STR.
  IF SY-SUBRC <> 0.
* Implement suitable error handling here
  ENDIF.

  IF I_OPENFILE IS NOT INITIAL.

    CALL FUNCTION 'WS_EXECUTE'
      EXPORTING
        INFORM             = ' '
        PROGRAM            = LW_FILENAME
      EXCEPTIONS
        FRONTEND_ERROR     = 1
        NO_BATCH           = 2
        PROG_NOT_FOUND     = 3
        ILLEGAL_OPTION     = 4
        GUI_REFUSE_EXECUTE = 5
        OTHERS             = 6.

  ENDIF.




ENDFUNCTION.
