FUNCTION ZFM_BM_DF_FCHK_ADR_DISTRICT .
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_FIELD)
*"     REFERENCE(I_RECORD)
*"     REFERENCE(I_DF_FIELD) TYPE  ZST_BM_DF_FIELD
*"  EXPORTING
*"     REFERENCE(E_FIELD)
*"     REFERENCE(E_RETURN) TYPE  BAPIRET2
*"     REFERENCE(ET_FIELD_EC) TYPE  ZTT_BM_DF_FIELD_EC
*"----------------------------------------------------------------------

  DATA:
    LW_COUNTRY                TYPE LAND1,
    LW_CITYCODE               TYPE CITY_CODE,
    LW_CITYPCODE              TYPE CITYP_CODE.

* Init address data if need
  PERFORM 9999_INIT_ADR.

  LW_CITYPCODE = I_FIELD.

* Get region for checking
  CALL FUNCTION 'ZFM_BM_DF_FCHK_GRP_GET'
    EXPORTING
      I_DF_FIELD    = I_DF_FIELD
      I_FIELDCODE   = GC_FIELD_COUNTRY
    IMPORTING
      E_FCODE_VALUE = LW_COUNTRY.
  IF LW_COUNTRY = GC_COUNTRY_VN
  AND LW_CITYPCODE IS INITIAL.
    PERFORM DF_FIELD_SET_ERR_INIT
      USING I_FIELD
            I_DF_FIELD
      CHANGING E_RETURN
               ET_FIELD_EC.
  ENDIF.

* Get city for checking
  CALL FUNCTION 'ZFM_BM_DF_FCHK_GRP_GET'
    EXPORTING
      I_DF_FIELD    = I_DF_FIELD
      I_FIELDCODE   = GC_FIELD_CITY
    IMPORTING
      E_FCODE_VALUE = LW_CITYCODE.

  IF LW_CITYPCODE IS NOT INITIAL.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT':
      EXPORTING INPUT  = LW_CITYCODE
      IMPORTING OUTPUT = LW_CITYCODE,
      EXPORTING INPUT  = LW_CITYPCODE
      IMPORTING OUTPUT = LW_CITYPCODE.

    READ TABLE GT_DISTRICT TRANSPORTING NO FIELDS
      WITH KEY COUNTRY    = GC_COUNTRY_VN
               CITYP_CODE = LW_CITYPCODE BINARY SEARCH.
    IF SY-SUBRC IS INITIAL.
      READ TABLE GT_DISTRICT TRANSPORTING NO FIELDS
        WITH KEY COUNTRY    = GC_COUNTRY_VN
                 CITY_CODE  = LW_CITYCODE
                 CITYP_CODE = LW_CITYPCODE BINARY SEARCH.
      IF SY-SUBRC IS INITIAL.
        E_FIELD               = LW_CITYPCODE.
      ELSE.
*       Put error validate DB
        PERFORM DF_DISTRICT_SET_ERR_DB
          USING I_FIELD
                I_DF_FIELD
          CHANGING E_RETURN
                   ET_FIELD_EC.
      ENDIF.
    ELSE.
*     Put error validate DB
      PERFORM DF_FIELD_SET_ERR_VALIDATE_DB
        USING I_FIELD
              I_DF_FIELD
        CHANGING E_RETURN
                 ET_FIELD_EC.
    ENDIF.

  ENDIF.

* Put value for next step checking
  CALL FUNCTION 'ZFM_BM_DF_FCHK_GRP_PUT'
    EXPORTING
      I_FIELD           = I_FIELD
      I_DF_FIELD        = I_DF_FIELD
      I_FIELDCODE       = GC_FIELD_DISTRICT.

ENDFUNCTION.
