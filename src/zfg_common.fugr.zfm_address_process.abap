FUNCTION ZFM_ADDRESS_PROCESS.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_REPL_SPACE) TYPE  XMARK OPTIONAL
*"  CHANGING
*"     REFERENCE(T_ADDRESS) TYPE  ZTT_ADDRESS
*"--------------------------------------------------------------------
DATA:
    LT_REGION     TYPE TABLE OF ZST_REGION,
    LS_REGION     TYPE ZST_REGION,
    LT_CITY       TYPE TABLE OF ZST_CITY,
    LS_CITY       TYPE ZST_CITY,
    LT_DISTRICT   TYPE TABLE OF ZST_DISTRICT,
    LS_DISTRICT   TYPE ZST_DISTRICT,
    LW_STREET     TYPE AD_STREET,
    LW_REPL_SPACE TYPE XMARK.
  FIELD-SYMBOLS:
    <LF_ADDRESS>  TYPE ZST_BM_ADDRESS.

  CHECK:T_ADDRESS[] IS NOT INITIAL.
  IF I_REPL_SPACE IS NOT INITIAL.
    LW_REPL_SPACE = GC_XMARK.
  ELSE.
    CALL FUNCTION 'ZFM_VARIANT_GET'
      EXPORTING
        I_VAR_NAME        = 'ZVA_ADPR_REPLACE_SPACE'
      IMPORTING
        E_VAR_VALUE       = LW_REPL_SPACE
      EXCEPTIONS
        NOT_FOUND         = 1
        OTHERS            = 2.
    IF SY-SUBRC <> 0.
      LW_REPL_SPACE = GC_XMARK.
    ENDIF.
  ENDIF.

  SELECT T005S~LAND1 T005S~BLAND BEZEI
    FROM T005S LEFT OUTER JOIN T005U
      ON T005S~LAND1 = T005U~LAND1 AND T005S~BLAND = T005U~BLAND
     AND T005U~SPRAS = SY-LANGU
    INTO TABLE LT_REGION.
  SORT LT_REGION BY BLAND.

  SELECT ADRCITY~COUNTRY REGION ADRCITY~CITY_CODE CITY_NAME CITY_SHORT
    FROM ADRCITY LEFT OUTER JOIN ADRCITYT
      ON ADRCITY~COUNTRY    = ADRCITYT~COUNTRY
     AND ADRCITY~CITY_CODE  = ADRCITYT~CITY_CODE
     AND ADRCITYT~LANGU     = SY-LANGU
    INTO TABLE LT_CITY
     FOR ALL ENTRIES IN T_ADDRESS
   WHERE ADRCITY~CITY_CODE = T_ADDRESS-CITY_NO.
  SORT LT_CITY BY CITY_CODE.

  SELECT COUNTRY CITY_CODE CITYP_CODE CITY_PART
    FROM ADRCITYPRT
    INTO TABLE LT_DISTRICT
     FOR ALL ENTRIES IN T_ADDRESS
   WHERE CITY_CODE = T_ADDRESS-CITY_NO
     AND CITYP_CODE = T_ADDRESS-DISTRCT_NO.
  SORT LT_DISTRICT BY CITY_CODE CITYP_CODE.

  LOOP AT T_ADDRESS ASSIGNING <LF_ADDRESS>.
    READ TABLE LT_REGION INTO LS_REGION
      WITH KEY BLAND = <LF_ADDRESS>-REGION BINARY SEARCH.
    IF SY-SUBRC IS INITIAL.
      <LF_ADDRESS>-BEZEI = LS_REGION-BEZEI.
      IF <LF_ADDRESS>-DELPREF IS NOT INITIAL.
        IF LW_REPL_SPACE = GC_XMARK.
          REPLACE TEXT-R01 IN <LF_ADDRESS>-BEZEI
            WITH SPACE IGNORING CASE.
          REPLACE TEXT-R02 IN <LF_ADDRESS>-BEZEI
            WITH SPACE IGNORING CASE.
        ELSE.
          REPLACE TEXT-R01 IN <LF_ADDRESS>-BEZEI
            WITH TEXT-RR1 IGNORING CASE.
          REPLACE TEXT-R02 IN <LF_ADDRESS>-BEZEI
            WITH TEXT-RR2 IGNORING CASE.
          REPLACE '. ' IN <LF_ADDRESS>-BEZEI
            WITH '.' IGNORING CASE.
        ENDIF.
        CONDENSE <LF_ADDRESS>-BEZEI.
      ENDIF.
    ENDIF.

    READ TABLE LT_CITY INTO LS_CITY
      WITH KEY CITY_CODE = <LF_ADDRESS>-CITY_NO BINARY SEARCH.
    IF SY-SUBRC IS INITIAL.
      IF <LF_ADDRESS>-DELPREF IS INITIAL.
        <LF_ADDRESS>-CITY = LS_CITY-CITY_NAME.
      ELSE.
        IF LW_REPL_SPACE = GC_XMARK.
          <LF_ADDRESS>-CITY = LS_CITY-CITY_SHORT.
        ELSE.
          <LF_ADDRESS>-CITY = LS_CITY-CITY_NAME.
          REPLACE TEXT-C01 IN <LF_ADDRESS>-CITY
            WITH TEXT-CR1 IGNORING CASE.
          REPLACE TEXT-C02 IN <LF_ADDRESS>-CITY
            WITH TEXT-CR2 IGNORING CASE.
          REPLACE TEXT-C03 IN <LF_ADDRESS>-CITY
            WITH TEXT-CR3 IGNORING CASE.
          REPLACE TEXT-C04 IN <LF_ADDRESS>-CITY
            WITH TEXT-CR4 IGNORING CASE.
          REPLACE '. ' IN <LF_ADDRESS>-CITY
            WITH '.' IGNORING CASE.
        ENDIF.
        CONDENSE <LF_ADDRESS>-CITY.
      ENDIF.
    ENDIF.

    READ TABLE LT_DISTRICT INTO LS_DISTRICT
      WITH KEY CITY_CODE = <LF_ADDRESS>-CITY_NO
               CITYP_CODE = <LF_ADDRESS>-DISTRCT_NO BINARY SEARCH.
    IF SY-SUBRC IS INITIAL.
      <LF_ADDRESS>-DISTRICT = LS_DISTRICT-CITY_PART.
      IF <LF_ADDRESS>-DELPREF IS NOT INITIAL.
        IF LW_REPL_SPACE = GC_XMARK.
          REPLACE TEXT-D01 IN <LF_ADDRESS>-DISTRICT
            WITH SPACE IGNORING CASE.
          REPLACE TEXT-D02 IN <LF_ADDRESS>-DISTRICT
            WITH SPACE IGNORING CASE.
          REPLACE TEXT-D03 IN <LF_ADDRESS>-DISTRICT
            WITH SPACE IGNORING CASE.
        ELSE.
          REPLACE TEXT-D01 IN <LF_ADDRESS>-DISTRICT
            WITH TEXT-DR1 IGNORING CASE.
          REPLACE TEXT-D02 IN <LF_ADDRESS>-DISTRICT
            WITH TEXT-DR2 IGNORING CASE.
          REPLACE TEXT-D03 IN <LF_ADDRESS>-DISTRICT
            WITH TEXT-DR3 IGNORING CASE.
          REPLACE '. ' IN <LF_ADDRESS>-DISTRICT
            WITH '.' IGNORING CASE.
        ENDIF.
        CONDENSE <LF_ADDRESS>-DISTRICT.
        IF <LF_ADDRESS>-DISTRICT = <LF_ADDRESS>-CITY.
          <LF_ADDRESS>-DISTRICT = LS_DISTRICT-CITY_PART.
        ENDIF.
      ENDIF.
    ENDIF.

    IF <LF_ADDRESS>-SHORTA IS INITIAL
    OR ( <LF_ADDRESS>-DISTRICT IS INITIAL
      AND <LF_ADDRESS>-CITY IS INITIAL
      AND <LF_ADDRESS>-BEZEI IS INITIAL ).
      LW_STREET = <LF_ADDRESS>-STREET.
    ELSE.
      CLEAR: LW_STREET.
    ENDIF.

    CALL FUNCTION 'ZFM_DATA_CONCATENATE'
      EXPORTING
        I_TEXT1           = LW_STREET
        I_TEXT2           = <LF_ADDRESS>-DISTRICT
        I_TEXT3           = <LF_ADDRESS>-CITY
        I_TEXT4           = <LF_ADDRESS>-BEZEI
      IMPORTING
        E_TEXT            = <LF_ADDRESS>-ADDRESS.
  ENDLOOP.





ENDFUNCTION.
