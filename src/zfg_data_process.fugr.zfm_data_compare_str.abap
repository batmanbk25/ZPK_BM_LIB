FUNCTION ZFM_DATA_COMPARE_STR.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_NEW_DATA) TYPE  ANY
*"     REFERENCE(I_OLD_DATA) TYPE  ANY
*"     REFERENCE(I_STRUCTURE) TYPE  TABNAME
*"  EXPORTING
*"     REFERENCE(E_CHANGE_X) TYPE  ANY
*"     REFERENCE(E_DIFFERENT) TYPE  XMARK
*"     REFERENCE(ET_CHANGE_FCAT) TYPE  LVC_T_FCAT
*"  EXCEPTIONS
*"      NO_STRUCTURE
*"--------------------------------------------------------------------
DATA:
    LT_FIELDCAT     TYPE LVC_T_FCAT,
    LS_FIELDCAT     TYPE LVC_S_FCAT.
  FIELD-SYMBOLS:
    <LF_FIELD_X>    TYPE ANY,
    <LF_FIELD_NEW>  TYPE ANY,
    <LF_FIELD_OLD>  TYPE ANY.

  CLEAR: E_DIFFERENT, E_CHANGE_X, ET_CHANGE_FCAT.
  IF I_STRUCTURE IS INITIAL.
    RAISE NO_STRUCTURE.
  ENDIF.

  READ TABLE GT_FCAT_COMPARE TRANSPORTING NO FIELDS
    WITH KEY TABNAME = I_STRUCTURE BINARY SEARCH.
  IF SY-SUBRC IS NOT INITIAL.
    CLEAR: GT_FCAT_COMPARE.
*   Get table name and field category
    CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
      EXPORTING
        I_STRUCTURE_NAME       = I_STRUCTURE
        I_INTERNAL_TABNAME     = I_STRUCTURE
      CHANGING
        CT_FIELDCAT            = GT_FCAT_COMPARE
      EXCEPTIONS
        INCONSISTENT_INTERFACE = 1
        PROGRAM_ERROR          = 2
        OTHERS                 = 3.
  ENDIF.
  LT_FIELDCAT = GT_FCAT_COMPARE.

  LOOP AT LT_FIELDCAT INTO LS_FIELDCAT.
    ASSIGN COMPONENT LS_FIELDCAT-FIELDNAME OF STRUCTURE I_NEW_DATA
      TO <LF_FIELD_NEW>.
    IF SY-SUBRC IS INITIAL.
      ASSIGN COMPONENT LS_FIELDCAT-FIELDNAME OF STRUCTURE I_OLD_DATA
        TO <LF_FIELD_OLD>.
      IF SY-SUBRC IS INITIAL
      AND <LF_FIELD_OLD> <> <LF_FIELD_NEW>.
        E_DIFFERENT = 'X'.
        IF E_CHANGE_X IS REQUESTED.
          ASSIGN COMPONENT LS_FIELDCAT-FIELDNAME OF STRUCTURE E_CHANGE_X
            TO <LF_FIELD_X>.
          IF SY-SUBRC IS INITIAL.
            <LF_FIELD_X> = 'X'.
          ENDIF.
        ELSEIF ET_CHANGE_FCAT IS REQUESTED.
          APPEND LS_FIELDCAT TO ET_CHANGE_FCAT.
        ELSE.
          RETURN.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.





ENDFUNCTION.
