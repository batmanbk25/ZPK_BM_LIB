FUNCTION ZFM_DATTAB_GET_DESC.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_TABLE) TYPE  TABNAME
*"  EXPORTING
*"     REFERENCE(T_TABLE_DATA) TYPE  ANY TABLE
*"----------------------------------------------------------------------
  DATA:
    LT_KEYS     TYPE TABLE OF FIELDNAME,
    LS_TAB_DESC TYPE ZTB_BM_TAB_DESC,
    LREF_DATA   TYPE REF TO DATA,
    BEGIN OF LS_DESTABLE,
      FNAME    TYPE ZTB_BM_TAB_DESC-FNAME,
      TXTFD    TYPE ZTB_BM_TAB_DESC-TXTFD,
      TABDC    TYPE ZTB_BM_TAB_DESC-TABDC,
      KEYFS    TYPE ZTB_BM_TAB_DESC-KEYFS,
      TXTFS    TYPE ZTB_BM_TAB_DESC-TXTFS,
      LANGF    TYPE ZTB_BM_TAB_DESC-LANGF,
      REFTABLE TYPE REF TO DATA,
    END OF LS_DESTABLE,
    LT_DESTABLE  LIKE TABLE OF LS_DESTABLE,
    LW_FNAMES    TYPE STRING,
    LW_STR_WHERE TYPE STRING.
  FIELD-SYMBOLS:
    <LF_RECORD>    TYPE ANY,
    <LF_FIELD>     TYPE ANY,
    <LF_DESFIELD>  TYPE ANY,
    <LF_DESRECORD> TYPE ANY,
    <LFT_DESTABLE> TYPE STANDARD TABLE.

* Init
  IF GT_TAB_DESC IS NOT INITIAL.
    READ TABLE GT_TAB_DESC TRANSPORTING NO FIELDS
      WITH KEY TABNM = I_TABLE.
    IF SY-SUBRC IS INITIAL.
      SELECT *
        APPENDING TABLE GT_TAB_DESC
        FROM ZTB_BM_TAB_DESC
       WHERE TABNM = I_TABLE.
    ENDIF.
  ELSE.
    SELECT *
      APPENDING TABLE GT_TAB_DESC
      FROM ZTB_BM_TAB_DESC
     WHERE TABNM = I_TABLE.
  ENDIF.

  LOOP AT GT_TAB_DESC INTO LS_TAB_DESC
    WHERE TABNM = I_TABLE.
    CLEAR: LT_KEYS.
    CREATE DATA LREF_DATA TYPE (LS_TAB_DESC-TABDC).
    ASSIGN LREF_DATA->* TO <LF_DESRECORD>.
    APPEND LS_TAB_DESC-KEYFS TO LT_KEYS.
    CREATE DATA LREF_DATA TYPE STANDARD TABLE OF (LS_TAB_DESC-TABDC)
      WITH KEY (LT_KEYS).
    ASSIGN LREF_DATA->* TO <LFT_DESTABLE>.

    LOOP AT T_TABLE_DATA ASSIGNING <LF_RECORD>.
      ASSIGN COMPONENT LS_TAB_DESC-FNAME OF STRUCTURE <LF_RECORD>
        TO <LF_FIELD>.
      IF SY-SUBRC IS INITIAL
      AND <LF_FIELD> IS NOT INITIAL.
        CLEAR: <LF_DESRECORD>.
        ASSIGN COMPONENT LS_TAB_DESC-KEYFS
          OF STRUCTURE <LF_DESRECORD> TO <LF_DESFIELD>.
        IF SY-SUBRC IS INITIAL.
          <LF_DESFIELD> = <LF_FIELD>.
          APPEND <LF_DESRECORD> TO <LFT_DESTABLE>.
        ENDIF.
      ENDIF.
    ENDLOOP.

    MOVE-CORRESPONDING LS_TAB_DESC TO LS_DESTABLE.
    GET REFERENCE OF <LFT_DESTABLE> INTO LS_DESTABLE-REFTABLE.
    APPEND LS_DESTABLE TO LT_DESTABLE.
  ENDLOOP.

  LOOP AT LT_DESTABLE INTO LS_DESTABLE.
    CLEAR: LW_STR_WHERE.
    ASSIGN LS_DESTABLE-REFTABLE->* TO <LFT_DESTABLE>.
    CHECK <LFT_DESTABLE> IS NOT INITIAL.

    CALL FUNCTION 'ZFM_DATA_CONCATENATE'
      EXPORTING
        I_TEXT1     = LS_DESTABLE-KEYFS
        I_TEXT2     = LS_DESTABLE-TXTFS
        I_SEPARATOR = ' '
      IMPORTING
        E_TEXT      = LW_FNAMES.

*    LW_STR_WHERE = 'KEYFS = <LFT_DESTABLE>-KEYFS'.
    CALL FUNCTION 'ZFM_DATA_CONCATENATE'
      EXPORTING
        I_TEXT1     = '<LFT_DESTABLE>'
        I_TEXT2     = LS_DESTABLE-KEYFS
        I_SEPARATOR = '-'
      IMPORTING
        E_TEXT      = LW_STR_WHERE.
    CALL FUNCTION 'ZFM_DATA_CONCATENATE'
      EXPORTING
        I_TEXT1     = LS_DESTABLE-KEYFS
        I_TEXT2     = LW_STR_WHERE
        I_SEPARATOR = ' = '
      IMPORTING
        E_TEXT      = LW_STR_WHERE.

    IF LS_DESTABLE-LANGF IS NOT INITIAL.
      CONCATENATE LW_STR_WHERE ' AND ' LS_DESTABLE-LANGF ' = SY-LANGU'
        INTO LW_STR_WHERE SEPARATED BY SPACE.
    ENDIF.

    SELECT * "(LW_FNAMES)
      FROM (LS_DESTABLE-TABDC)
      INTO CORRESPONDING FIELDS OF TABLE <LFT_DESTABLE>
       FOR ALL ENTRIES IN <LFT_DESTABLE>
     WHERE (LW_STR_WHERE).

    SORT <LFT_DESTABLE> BY (LS_DESTABLE-KEYFS).
  ENDLOOP.


  LOOP AT T_TABLE_DATA ASSIGNING <LF_RECORD>.
    LOOP AT LT_DESTABLE INTO LS_DESTABLE.
      ASSIGN COMPONENT LS_DESTABLE-FNAME OF STRUCTURE <LF_RECORD>
        TO <LF_FIELD>.
      IF SY-SUBRC IS INITIAL.
        ASSIGN LS_DESTABLE-REFTABLE->* TO <LFT_DESTABLE>.
        READ TABLE <LFT_DESTABLE> ASSIGNING <LF_DESRECORD>
          WITH KEY (LS_DESTABLE-KEYFS) = <LF_FIELD> BINARY SEARCH.
        IF SY-SUBRC IS INITIAL.
          ASSIGN COMPONENT LS_DESTABLE-TXTFD OF STRUCTURE <LF_RECORD>
            TO <LF_FIELD>.
          ASSIGN COMPONENT LS_DESTABLE-TXTFS
            OF STRUCTURE <LF_DESRECORD> TO <LF_DESFIELD>.
          IF SY-SUBRC IS INITIAL.
            <LF_FIELD> = <LF_DESFIELD>.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

ENDFUNCTION.
