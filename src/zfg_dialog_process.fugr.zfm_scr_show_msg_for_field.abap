FUNCTION ZFM_SCR_SHOW_MSG_FOR_FIELD.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_RETURN) TYPE  ZST_ERR_FIELD
*"     REFERENCE(I_FIELDTEXT) OPTIONAL
*"     REFERENCE(I_FIELD) OPTIONAL
*"     REFERENCE(I_GETFTEXT) TYPE  XMARK OPTIONAL
*"     REFERENCE(I_CPROG) TYPE  SYREPID DEFAULT SY-CPROG
*"  EXPORTING
*"     REFERENCE(E_DISABLE_F) TYPE  XMARK
*"--------------------------------------------------------------------
DATA:
    LS_FIELD_DB   TYPE ZTB_FIELD_DB,
    LW_TABNAME    TYPE TABNAME,
    LW_SCRTEXT_L  TYPE SCRTEXT_L,
    LW_FIELDNAME  TYPE CHAR61,
    LS_DFIES      TYPE DFIES,
    LS_DD04V      TYPE DD04V.


  LOOP AT SCREEN.
    IF SCREEN-NAME = I_RETURN-FIELD
    AND SCREEN-INPUT = '0'.
      E_DISABLE_F = GC_XMARK.
      RETURN.
    ENDIF.
  ENDLOOP.

  IF I_RETURN-FIELD IS NOT INITIAL.
    LW_FIELDNAME = I_RETURN-FIELD.
  ELSEIF I_FIELD IS SUPPLIED.
*   Get data type of field
    DESCRIBE FIELD I_FIELD HELP-ID LW_FIELDNAME.
  ENDIF.

* High light error field
  CALL FUNCTION 'ZFM_SCR_PAI_PUT_HIGHLIGHT_F'
    EXPORTING
      I_REPID                 = I_CPROG
      I_DYNNR                 = I_RETURN-DYNNR
      I_FIELDNAME             = LW_FIELDNAME
      I_ROW                   = I_RETURN-ROW.

* Show message with no field label
  IF I_GETFTEXT IS INITIAL.
    MESSAGE ID I_RETURN-ID TYPE 'S' NUMBER I_RETURN-NUMBER
      DISPLAY LIKE I_RETURN-TYPE
      WITH I_RETURN-MESSAGE_V1 I_RETURN-MESSAGE_V2
       I_RETURN-MESSAGE_V3 I_RETURN-MESSAGE_V4.
* Show message with field label
  ELSE.
*   Get Label of error field to show massage
    IF I_FIELDTEXT IS INITIAL.
      PERFORM GET_FIELD_LABEL_ABAPDIC
        USING     LW_FIELDNAME
        CHANGING  LW_SCRTEXT_L.
    ELSE.
      LW_SCRTEXT_L = I_FIELDTEXT.
    ENDIF.
    IF LW_SCRTEXT_L IS NOT INITIAL.
      MESSAGE ID I_RETURN-ID TYPE 'S' NUMBER I_RETURN-NUMBER
        DISPLAY LIKE I_RETURN-TYPE
        WITH LW_SCRTEXT_L I_RETURN-MESSAGE_V1 I_RETURN-MESSAGE_V2
         I_RETURN-MESSAGE_V3.
    ELSE.
      MESSAGE ID I_RETURN-ID TYPE 'S' NUMBER I_RETURN-NUMBER
        DISPLAY LIKE I_RETURN-TYPE
        WITH I_RETURN-MESSAGE_V1 I_RETURN-MESSAGE_V2
         I_RETURN-MESSAGE_V3 I_RETURN-MESSAGE_V4.
    ENDIF.
  ENDIF.



* Set cusor field
  IF I_RETURN-FIELD IS NOT INITIAL.
    LW_FIELDNAME = I_RETURN-FIELD.
  ENDIF.

  IF I_RETURN-ROW IS INITIAL.
    SET CURSOR FIELD LW_FIELDNAME.
  ELSE.
    SET CURSOR FIELD LW_FIELDNAME LINE I_RETURN-ROW.
  ENDIF.





ENDFUNCTION.
