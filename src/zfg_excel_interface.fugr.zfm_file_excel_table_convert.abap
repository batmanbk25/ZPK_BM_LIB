FUNCTION ZFM_FILE_EXCEL_TABLE_CONVERT.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_START_ROW) TYPE  I DEFAULT 1
*"     REFERENCE(I_START_COL) TYPE  I DEFAULT 1
*"     REFERENCE(I_GET_BOLD) TYPE  XMARK OPTIONAL
*"     REFERENCE(I_TABNAME) TYPE  TABNAME OPTIONAL
*"     REFERENCE(I_TRANSPOSE) TYPE  XMARK OPTIONAL
*"     REFERENCE(I_USE_COLPOS) TYPE  XMARK DEFAULT 'X'
*"     REFERENCE(I_KEEP_VALUE) TYPE  XMARK
*"  TABLES
*"      T_OUTTAB
*"      T_FIELDCAT TYPE  SLIS_T_FIELDCAT_ALV OPTIONAL
*"      T_EXCEL TYPE  ZTT_EXCEL
*"      T_BOLD_ROWS TYPE  ZTT_BOLD_ROW OPTIONAL
*"--------------------------------------------------------------------
DATA:
    LS_FIELDCAT     TYPE SLIS_FIELDCAT_ALV,
    LT_FIELDCAT     TYPE SLIS_T_FIELDCAT_ALV,
    LS_EXCEL_DATA   TYPE SOI_GENERIC_ITEM,
    LW_ROW          TYPE I,
    LW_COL          TYPE I,
    LW_FIELDPOS     TYPE I,
    LW_GET_BOLD     TYPE XMARK VALUE SPACE,
    LS_BOLD_FORMAT  TYPE I,
    LT_RETURN       TYPE TABLE OF BAPIRET2.
  FIELD-SYMBOLS:
    <LF_DATA>       TYPE ANY,
    <LF_VALUE>      TYPE ANY,
    <LF_CURKY>      TYPE ANY,
    <LF_BOLD>       TYPE ANY.

* Init
  LW_ROW = I_START_ROW.
  LW_COL = I_START_COL.
  REFRESH: T_EXCEL[], T_BOLD_ROWS[].

  IF I_TABNAME IS NOT INITIAL.
    CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
      EXPORTING
        I_STRUCTURE_NAME       = I_TABNAME
      CHANGING
        CT_FIELDCAT            = LT_FIELDCAT
      EXCEPTIONS
        INCONSISTENT_INTERFACE = 1
        PROGRAM_ERROR          = 2
        OTHERS                 = 3.
  ELSE.
    LT_FIELDCAT[] = T_FIELDCAT[].
  ENDIF.

  CHECK LT_FIELDCAT[] IS NOT INITIAL.

* Sort field category table
  SORT LT_FIELDCAT BY NO_OUT COL_POS.
  IF I_GET_BOLD = 'X'.
    READ TABLE LT_FIELDCAT WITH KEY FIELDNAME = GC_FIELD_BOLD
      TRANSPORTING NO FIELDS.
    IF SY-SUBRC IS INITIAL.
      LW_GET_BOLD = 'X'.
    ENDIF.
  ENDIF.


  CALL FUNCTION 'BAPI_USER_GET_DETAIL'
    EXPORTING
      USERNAME = SY-UNAME
    IMPORTING
      DEFAULTS = GS_DEFAULTS
    TABLES
      RETURN   = LT_RETURN.

* Process data
  LOOP AT T_OUTTAB ASSIGNING <LF_DATA>.
    LW_FIELDPOS = 0.
*   Process each cell
    LOOP AT LT_FIELDCAT INTO LS_FIELDCAT WHERE NO_OUT IS INITIAL.
*     Get cell position
      IF I_USE_COLPOS IS INITIAL.
        LW_FIELDPOS = LW_FIELDPOS + 1.
      ELSE.
        LW_FIELDPOS = LS_FIELDCAT-COL_POS.
      ENDIF.
*     Process cell at column name LS_FIELDCAT-FIELDNAME
      ASSIGN COMPONENT LS_FIELDCAT-FIELDNAME OF STRUCTURE <LF_DATA>
        TO <LF_VALUE>.
      IF SY-SUBRC IS INITIAL.
*       Get Row, column in excel file
        IF I_TRANSPOSE IS INITIAL.
          LW_COL = LW_FIELDPOS + I_START_COL - 1.
        ELSE.
          LW_ROW = LW_FIELDPOS + I_START_ROW - 1.
        ENDIF.
*       Set position
        LS_EXCEL_DATA-ROW     = LW_ROW.
        LS_EXCEL_DATA-COLUMN  = LW_COL.
*       Set value
        PERFORM STANDARD_VALUE
          USING     LS_FIELDCAT <LF_DATA> <LF_VALUE> I_KEEP_VALUE
          CHANGING  LS_EXCEL_DATA-VALUE.

        CONDENSE: LS_EXCEL_DATA-ROW, LS_EXCEL_DATA-COLUMN,
                  LS_EXCEL_DATA-VALUE.
        APPEND LS_EXCEL_DATA TO T_EXCEL.
      ENDIF.
    ENDLOOP.
    IF LW_GET_BOLD = 'X'.
      ASSIGN COMPONENT GC_FIELD_BOLD OF STRUCTURE <LF_DATA>
        TO <LF_BOLD>.
      IF <LF_BOLD> IS NOT INITIAL.
        T_BOLD_ROWS-ROW = LW_ROW.
        APPEND T_BOLD_ROWS.
      ENDIF.
    ENDIF.

    IF I_TRANSPOSE IS INITIAL.
      LW_ROW = LW_ROW + 1.
    ELSE.
      LW_COL = LW_COL + 1.
    ENDIF.
  ENDLOOP.





ENDFUNCTION.
