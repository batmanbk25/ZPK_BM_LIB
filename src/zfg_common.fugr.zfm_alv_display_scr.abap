FUNCTION ZFM_ALV_DISPLAY_SCR.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_CPROG) TYPE  CPROG DEFAULT SY-CPROG
*"     REFERENCE(I_DYNNR) TYPE  DYNNR DEFAULT SY-DYNNR
*"     REFERENCE(I_CUS_CONTROL_NAME) TYPE  SCRFNAME OPTIONAL
*"     REFERENCE(I_EXTENSION) TYPE  INT4 DEFAULT 100
*"     REFERENCE(I_SIDE) TYPE  I OPTIONAL
*"     REFERENCE(I_LASTCOL) TYPE  I OPTIONAL
*"     REFERENCE(I_BUFFER_ACTIVE) TYPE  ANY OPTIONAL
*"     REFERENCE(I_BYPASSING_BUFFER) TYPE  CHAR01 OPTIONAL
*"     REFERENCE(I_CONSISTENCY_CHECK) TYPE  CHAR1 OPTIONAL
*"     REFERENCE(I_STRUCTURE_NAME) TYPE  DD02L-TABNAME OPTIONAL
*"     VALUE(IS_VARIANT) TYPE  DISVARIANT OPTIONAL
*"     VALUE(I_SAVE) TYPE  CHAR01 DEFAULT 'U'
*"     REFERENCE(I_DEFAULT) TYPE  CHAR01 DEFAULT 'X'
*"     VALUE(IS_LAYOUT) TYPE  LVC_S_LAYO OPTIONAL
*"     REFERENCE(IS_PRINT) TYPE  LVC_S_PRNT OPTIONAL
*"     REFERENCE(IT_SPECIAL_GROUPS) TYPE  LVC_T_SGRP OPTIONAL
*"     REFERENCE(IT_TOOLBAR_EXCLUDING) TYPE  UI_FUNCTIONS OPTIONAL
*"     REFERENCE(IT_HYPERLINK) TYPE  LVC_T_HYPE OPTIONAL
*"     REFERENCE(IT_ALV_GRAPHICS) TYPE  DTC_T_TC OPTIONAL
*"     REFERENCE(IT_EXCEPT_QINFO) TYPE  LVC_T_QINF OPTIONAL
*"     REFERENCE(IR_SALV_ADAPTER) TYPE REF TO  IF_SALV_ADAPTER OPTIONAL
*"     REFERENCE(I_SHOW_TOTAL_INFO) TYPE  XMARK DEFAULT SPACE
*"     REFERENCE(I_SHOW_ERRBTN) TYPE  XMARK OPTIONAL
*"     REFERENCE(IT_ERRFIELD_TITLE) TYPE  LVC_T_FCAT OPTIONAL
*"     REFERENCE(I_HIDE_ERRLINE) TYPE  XMARK OPTIONAL
*"     REFERENCE(I_CALLBACK_PROGRAM) TYPE  SY-REPID OPTIONAL
*"     REFERENCE(I_CALLBACK_BUTTON_CLICK) TYPE  SLIS_FORMNAME OPTIONAL
*"     REFERENCE(I_CALLBACK_HOSPOT_CLICK) TYPE  SLIS_FORMNAME OPTIONAL
*"     REFERENCE(IT_HEADER) TYPE  ZTT_ALV_HEADER OPTIONAL
*"     REFERENCE(I_LOGO) TYPE  ZST_BM_ALV_LOGO OPTIONAL
*"     REFERENCE(I_GUI_CONTAINER) TYPE REF TO  CL_GUI_CONTAINER
*"       OPTIONAL
*"     REFERENCE(I_CUSTOMIZE_ALV) TYPE  XMARK OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_ALV_GRID) TYPE REF TO  CL_GUI_ALV_GRID
*"     REFERENCE(E_DOCKING) TYPE REF TO  CL_GUI_DOCKING_CONTAINER
*"     REFERENCE(E_CUS_CONTAINER) TYPE REF TO  CL_GUI_CUSTOM_CONTAINER
*"     REFERENCE(E_ALV_HEADER_DOC) TYPE REF TO  CL_DD_DOCUMENT
*"     REFERENCE(E_EVENT_RECEIVER) TYPE REF TO  ZCL_BM_ALV_HANDLE
*"     REFERENCE(E_ALV_GRID_MERGE) TYPE REF TO  ZCL_GUI_ALV_GRID_MERGE
*"  CHANGING
*"     REFERENCE(IT_OUTTAB) TYPE  STANDARD TABLE OPTIONAL
*"     REFERENCE(IT_FIELDCATALOG) TYPE  LVC_T_FCAT OPTIONAL
*"     REFERENCE(IT_SORT) TYPE  LVC_T_SORT OPTIONAL
*"     REFERENCE(IT_FILTER) TYPE  LVC_T_FILT OPTIONAL
*"----------------------------------------------------------------------
  DATA: LT_FIELDCAT TYPE LVC_T_FCAT.

  GW_CALLBACK_PROGRAM       = I_CALLBACK_PROGRAM.
  GW_CALLBACK_BUTTON_CLICK  = I_CALLBACK_BUTTON_CLICK.
  GW_CALLBACK_HOSPOT_CLICK  = I_CALLBACK_HOSPOT_CLICK.
  GW_HIDE_ERRLINE           = I_HIDE_ERRLINE.

* Create ALV objects
  PERFORM CREATE_ALV_OBJECTS
    USING I_EXTENSION
          I_SIDE
          I_CUS_CONTROL_NAME
          I_CPROG
          I_DYNNR
          I_GUI_CONTAINER
          IT_HEADER
          I_LOGO
          I_CUSTOMIZE_ALV.

* Create Field catalog
  PERFORM CREATE_FIELDCATALOG
    USING I_STRUCTURE_NAME
          IT_FIELDCATALOG[]
          I_LASTCOL
          I_BYPASSING_BUFFER
          I_SHOW_ERRBTN
 CHANGING LT_FIELDCAT.

* Set disable if need
  PERFORM SET_DISABLE_ALV_ROW
    USING     IS_LAYOUT
    CHANGING  IT_OUTTAB.

* Set default Layout, variant
  PERFORM ALV_SET_VARIANT_LAYOUT
    CHANGING IS_LAYOUT
             IS_VARIANT
             I_SAVE.

  CALL METHOD GO_ALV_GRID->REGISTER_EDIT_EVENT
    EXPORTING
      I_EVENT_ID = CL_GUI_ALV_GRID=>MC_EVT_ENTER.

* First display
  CALL METHOD GO_ALV_GRID->SET_TABLE_FOR_FIRST_DISPLAY
    EXPORTING
      IS_VARIANT                    = IS_VARIANT
      I_SAVE                        = I_SAVE
      I_DEFAULT                     = I_DEFAULT
      IS_LAYOUT                     = IS_LAYOUT
      IS_PRINT                      = IS_PRINT
      IT_SPECIAL_GROUPS             = IT_SPECIAL_GROUPS
      IT_TOOLBAR_EXCLUDING          = IT_TOOLBAR_EXCLUDING
      IT_HYPERLINK                  = IT_HYPERLINK
      IT_ALV_GRAPHICS               = IT_ALV_GRAPHICS
      IT_EXCEPT_QINFO               = IT_EXCEPT_QINFO
      IR_SALV_ADAPTER               = IR_SALV_ADAPTER
    CHANGING
      IT_OUTTAB                     = IT_OUTTAB
      IT_FIELDCATALOG               = LT_FIELDCAT "IT_FIELDCATALOG
      IT_SORT                       = IT_SORT
      IT_FILTER                     = IT_FILTER
    EXCEPTIONS
      INVALID_PARAMETER_COMBINATION = 1
      PROGRAM_ERROR                 = 2
      TOO_MANY_LINES                = 3
      OTHERS                        = 4.

* Set export parametters
  IF E_ALV_GRID IS REQUESTED.
    E_ALV_GRID       = GO_ALV_GRID.
  ENDIF.

  IF E_ALV_GRID_MERGE IS REQUESTED.
    E_ALV_GRID_MERGE = GO_ALV_GRID_MERGE.
  ENDIF.

  IF I_CUS_CONTROL_NAME IS INITIAL.
    E_DOCKING  = GO_ALV_DOCKING.
  ELSE.
    E_CUS_CONTAINER  = GO_ALV_CUS_CONTAINER.
  ENDIF.

  IF E_ALV_HEADER_DOC IS REQUESTED.
    E_ALV_HEADER_DOC = GO_ALV_HEADER_DOC.
  ENDIF.

  ASSIGN IT_OUTTAB TO <GT_ALV_DATA>.

  IF I_HIDE_ERRLINE IS NOT INITIAL.
    PERFORM ALV_SET_HIDE_FILTER.
  ENDIF.

  IF I_SHOW_TOTAL_INFO IS NOT INITIAL.
    PERFORM SHOW_ALV_TOTAL_RECORD USING IT_OUTTAB.
  ENDIF.
  CREATE OBJECT GO_EVENT_RECEIVER.
  GO_EVENT_RECEIVER->GW_CALLBACK_PROGRAM      = I_CALLBACK_PROGRAM.
  GO_EVENT_RECEIVER->GW_CALLBACK_BUTTON_CLICK = I_CALLBACK_BUTTON_CLICK.
  GO_EVENT_RECEIVER->GW_CALLBACK_HOSPOT_CLICK = I_CALLBACK_HOSPOT_CLICK.
  GO_EVENT_RECEIVER->GW_HIDE_ERRLINE          = I_HIDE_ERRLINE.
  GO_EVENT_RECEIVER->GT_ERRFIELD_TITLE        = IT_ERRFIELD_TITLE.
  GET REFERENCE OF IT_OUTTAB INTO GO_EVENT_RECEIVER->GT_ALV_DATA.
  E_EVENT_RECEIVER = GO_EVENT_RECEIVER.

  IF I_SHOW_TOTAL_INFO IS NOT INITIAL
  OR I_HIDE_ERRLINE IS NOT INITIAL.
    SET HANDLER GO_EVENT_RECEIVER->HANDLE_TOOLBAR FOR GO_ALV_GRID.
    SET HANDLER GO_EVENT_RECEIVER->HANDLE_USER_COMMAND FOR GO_ALV_GRID.
  ENDIF.
  IF I_SHOW_ERRBTN IS NOT INITIAL
  OR I_CALLBACK_HOSPOT_CLICK IS NOT INITIAL.
*   Set Event handle Button click
*    SET HANDLER GO_EVENT_RECEIVER->HANDLE_BUTTON_CLICK FOR GO_ALV_GRID.
*    SET HANDLER GO_EVENT_RECEIVER->HANDLE_LINK_CLICK FOR GO_ALV_GRID.
    SET HANDLER GO_EVENT_RECEIVER->HANDLE_HOTSPOT_CLICK
    FOR GO_ALV_GRID.
  ENDIF.
  CALL METHOD GO_ALV_GRID->SET_TOOLBAR_INTERACTIVE.
  IF I_CALLBACK_BUTTON_CLICK IS NOT INITIAL.
*   Set Event handle Button click
    SET HANDLER GO_EVENT_RECEIVER->HANDLE_BUTTON_CLICK FOR GO_ALV_GRID.
  ENDIF.

ENDFUNCTION.
