FUNCTION ZFM_FILE_EXCEL_IMP2TAB_ATM.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_LOCALFILE)
*"     REFERENCE(I_CLOSEFILE) TYPE  XMARK OPTIONAL
*"     REFERENCE(I_READING_LINE) TYPE  INT4 DEFAULT 100
*"     REFERENCE(I_READING_SHEET) TYPE  INT4 DEFAULT 1
*"     REFERENCE(I_SHEET_FIELD) TYPE  FIELDNAME OPTIONAL
*"     REFERENCE(I_ROWIXFIELD) TYPE  FIELDNAME OPTIONAL
*"     REFERENCE(I_ROW) TYPE  I DEFAULT 1
*"     REFERENCE(I_COL) TYPE  I DEFAULT 0
*"     REFERENCE(I_ROW_HD) TYPE  I DEFAULT 0
*"  EXPORTING
*"     REFERENCE(T_IMPTAB) TYPE  ANY TABLE
*"     REFERENCE(T_EXCEL_MAPPING) TYPE  ZTT_EXCEL_MAPPING
*"  CHANGING
*"     REFERENCE(T_FIELDCAT) TYPE  LVC_T_FCAT
*"  EXCEPTIONS
*"      OPENFILE_ERROR
*"      NO_MAPPING
*"      READ_DATA_ERROR
*"      MAPPING_ERROR
*"----------------------------------------------------------------------
  DATA:
    LT_SHEETDATA    TYPE GTY_T_SHEETDATA,
    LS_SHEETDATA    TYPE GTY_SHEETDATA,
    LS_SHEET        TYPE SOI_SHEETS,
    LW_FILENAME     TYPE LOCALFILE,
    LT_HDR_ROW_DATA TYPE ZTT_EXCEL_NUMBR,
    LS_SHEET_ROWST  TYPE ZST_SHEET_ROWST,
    LT_SHEET_ROWST  TYPE TABLE OF ZST_SHEET_ROWST.

* Open file
  LW_FILENAME = I_LOCALFILE.
  CALL FUNCTION 'ZFM_DOI_EXCEL_OPEN'
    EXPORTING
      I_FILENAME     = LW_FILENAME
      I_READ_ONLY    = 'X'
    EXCEPTIONS
      OPEN_FILE_ERR  = 1
      OPEN_SHEET_ERR = 2
      OTHERS         = 3.
  IF SY-SUBRC <> 0.
    RAISE OPENFILE_ERROR.
  ENDIF.

  CHECK GT_SHEETS[] IS NOT INITIAL.

* Prepare sheet info
*--------------------------------------------------------------------*
  " Changed by NgocNV8 - 28/07/2016
  " Loop for prepate multi sheet
  REFRESH: T_EXCEL_MAPPING[].
  LOOP AT GT_SHEETS INTO LS_SHEET.
    IF SY-TABIX > I_READING_SHEET.
      EXIT.
    ENDIF.
    LS_SHEET_ROWST-SHEETNAME = LS_SHEET-SHEET_NAME.
    LS_SHEET_ROWST-ROWST  = I_ROW + 1.
    IF I_COL = 0.
      LS_SHEET_ROWST-COLS   = LINES( T_FIELDCAT ).
    ELSE.
      LS_SHEET_ROWST-COLS   = I_COL .
    ENDIF.

    APPEND LS_SHEET_ROWST TO LT_SHEET_ROWST.

    " Get header row data
    PERFORM GET_ONE_EXCEL_ROW
      USING GO_SPREADSHEET
            LS_SHEET
            I_ROW
            LS_SHEET_ROWST-COLS
      CHANGING LT_HDR_ROW_DATA.

    DELETE LT_HDR_ROW_DATA WHERE VALUE IS INITIAL.

    " Mapping columns using header row and fieldcat (Matching fieldname)
    PERFORM GEN_COLUMN_MAPPING
      USING LS_SHEET
            LT_HDR_ROW_DATA
      CHANGING  T_FIELDCAT
                T_EXCEL_MAPPING.
  ENDLOOP.

  IF T_EXCEL_MAPPING[] IS INITIAL.
    " Close file
    CALL FUNCTION 'ZFM_DOI_EXCEL_CLOSE'
      EXPORTING
        I_FILENAME  = LW_FILENAME
        I_OPEN_FILE = ''.
    RETURN.
  ENDIF.
  CHECK T_EXCEL_MAPPING[] IS NOT INITIAL.
*--------------------------------------------------------------------*

* Get data
  CLEAR: LT_SHEETDATA[].
  PERFORM GET_EXCEL_FILE_DATA
    USING     LT_SHEET_ROWST
              T_EXCEL_MAPPING
              I_READING_LINE
    CHANGING  LT_SHEETDATA.

* Close file
  CALL FUNCTION 'ZFM_DOI_EXCEL_CLOSE'
    EXPORTING
      I_FILENAME  = LW_FILENAME
      I_OPEN_FILE = ''.

* Convert data to table

  LOOP AT LT_SHEETDATA INTO LS_SHEETDATA.
    DELETE LS_SHEETDATA-SHEETDATA WHERE ROW < I_ROW_HD.
    CALL FUNCTION 'ZFM_FILE_EXCEL_CONV2TAB_MAP'
      EXPORTING
        T_MAPPING    = T_EXCEL_MAPPING
        T_EXCEL      = LS_SHEETDATA-SHEETDATA
        T_FIELDCAT   = T_FIELDCAT
        I_SHEETNAME  = LS_SHEETDATA-SHTNM
        I_SHEETFIELD = I_SHEET_FIELD
        I_ROWIXFIELD = I_ROWIXFIELD
      IMPORTING
        T_OUTTAB     = T_IMPTAB.
  ENDLOOP.





ENDFUNCTION.
