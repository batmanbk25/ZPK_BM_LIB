FUNCTION ZFM_FILE_EXCEL_GET_HEADER.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_REPORT) TYPE  PROGRAMM DEFAULT SY-CPROG
*"     REFERENCE(I_TABNAME) TYPE  TABNAME OPTIONAL
*"     REFERENCE(I_HEADER)
*"     REFERENCE(T_EXCEL_LAYOUT) TYPE  ZTT_EXCEL_LAYOUT OPTIONAL
*"     REFERENCE(I_LARGE_FILE) TYPE  XMARK OPTIONAL
*"  EXPORTING
*"     REFERENCE(T_EXCEL_EXP) TYPE  ZTT_EXCEL_EXP
*"     REFERENCE(E_PAGESETUP) TYPE  ZST_EXCEL_PAGESETUP
*"--------------------------------------------------------------------
DATA:
        LT_EXCEL_HDR  TYPE TABLE OF ZTB_EXCEL_LAYOUT,
        LS_EXCEL_HDR  TYPE ZTB_EXCEL_LAYOUT,
        LT_EXCEL_ITM  TYPE TABLE OF ZTB_EXCEL_LAYOUT,
        LS_EXCEL_ITM  TYPE ZTB_EXCEL_LAYOUT,
        LS_EXCEL      TYPE ZST_EXCEL,
        LS_EXCELN     TYPE ZST_EXCEL_NUMBR,
        LS_EXCEL_EXP  TYPE ZST_EXCEL_EXP,
        LW_GRPEX      TYPE ZTB_EXCEL_HDR-GRPEX,
        LT_FIELDCAT   TYPE TABLE OF LVC_S_FCAT,
        LS_FIELDCAT   TYPE LVC_S_FCAT,
        LW_TABNAME    TYPE TABNAME,
        LW_ROWINS     TYPE I.
  FIELD-SYMBOLS:
    <LF_DATA>       TYPE ANY,
    <LFT_ITEMS>     TYPE ANY TABLE,
    <LF_PAGESETUP_F> TYPE ANY.

  LW_TABNAME = I_TABNAME.
  IF LW_TABNAME IS INITIAL.
    DESCRIBE FIELD I_HEADER HELP-ID LW_TABNAME.
  ENDIF.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      I_STRUCTURE_NAME   = LW_TABNAME
      I_INTERNAL_TABNAME = LW_TABNAME
    CHANGING
      CT_FIELDCAT        = LT_FIELDCAT
    EXCEPTIONS
      OTHERS             = 3.

* Get excel layout
  LT_EXCEL_HDR = T_EXCEL_LAYOUT.
  IF LT_EXCEL_HDR IS INITIAL.
    SELECT *
      INTO TABLE LT_EXCEL_HDR
      FROM ZTB_EXCEL_LAYOUT
     WHERE REPORT  = I_REPORT.
  ENDIF.
  LT_EXCEL_ITM = LT_EXCEL_HDR.
  DELETE LT_EXCEL_HDR WHERE IS_ITEM = 'X'.
  DELETE LT_EXCEL_ITM WHERE IS_ITEM <> 'X'.

  SORT LT_EXCEL_HDR BY GRPEX ROW_POS COL_POS.
  LOOP AT LT_EXCEL_HDR INTO LS_EXCEL_HDR.
    IF (   LS_EXCEL_HDR-GRPEX IS INITIAL
        OR LS_EXCEL_HDR-GRPEX <> LW_GRPEX )
    AND LS_EXCEL_EXP IS NOT INITIAL.
      IF I_LARGE_FILE IS NOT INITIAL.
*       EHP7 up
*        MOVE-CORRESPONDING LS_EXCEL_EXP-EXDAT TO LS_EXCEL_EXP-EXDATN.
*       EPH6 down
        CALL FUNCTION 'ZFM_DATA_TABLE_MOVE_CORRESPOND'
          CHANGING
            C_SRC_TAB       = LS_EXCEL_EXP-EXDAT
            C_DES_TAB       = LS_EXCEL_EXP-EXDATN.
      ENDIF.
      APPEND LS_EXCEL_EXP TO T_EXCEL_EXP.
      CLEAR LS_EXCEL_EXP.
    ENDIF.
    LW_GRPEX = LS_EXCEL_HDR-GRPEX.
    CLEAR: LS_EXCEL, LS_EXCELN.
    ASSIGN COMPONENT LS_EXCEL_HDR-FNAME OF STRUCTURE I_HEADER
      TO <LF_DATA>.
    IF LS_EXCEL_EXP-ROW_POS IS INITIAL.
      LS_EXCEL_EXP-ROW_POS    = LS_EXCEL_HDR-ROW_POS.
      LS_EXCEL_EXP-COL_POS    = LS_EXCEL_HDR-COL_POS.
    ENDIF.
*   Set position
    LS_EXCEL-ROW      = LS_EXCEL_HDR-ROW_POS.
    LS_EXCEL-COLUMN   = LS_EXCEL_HDR-COL_POS.
    LS_EXCELN-ROW     = LS_EXCEL_HDR-ROW_POS.
    LS_EXCELN-COLUMN  = LS_EXCEL_HDR-COL_POS.
*   Change position if item has insert rows
    LOOP AT LT_EXCEL_ITM INTO LS_EXCEL_ITM.
*     Set row
      IF LS_EXCEL_HDR-ROW_POS > LS_EXCEL_ITM-ROW_POS
      AND LS_EXCEL_ITM-INSERT_ROW = 'X'.
        ASSIGN COMPONENT LS_EXCEL_ITM-FNAME OF STRUCTURE I_HEADER
          TO <LFT_ITEMS>.
        DESCRIBE TABLE <LFT_ITEMS> LINES LW_ROWINS.
        IF LW_ROWINS > LS_EXCEL_ITM-INITROWS.
          LS_EXCEL-ROW = LS_EXCEL-ROW + LW_ROWINS
                       - LS_EXCEL_ITM-INITROWS.
          LS_EXCELN-ROW = LS_EXCELN-ROW + LW_ROWINS
                        - LS_EXCEL_ITM-INITROWS.
        ENDIF.
      ENDIF.

*     Set column
      IF LS_EXCEL_HDR-COL_POS > LS_EXCEL_ITM-COL_POS
      AND LS_EXCEL_ITM-INSERT_COL = 'X'.
        IF LS_EXCEL_ITM-NCOLS > LS_EXCEL_ITM-INITCOLS.
          LS_EXCEL-COLUMN = LS_EXCEL-COLUMN + LS_EXCEL_ITM-NCOLS
                          - LS_EXCEL_ITM-INITCOLS.
          LS_EXCELN-COLUMN  = LS_EXCELN-COLUMN + LS_EXCEL_ITM-NCOLS
                            - LS_EXCEL_ITM-INITCOLS.
        ENDIF.
      ENDIF.
    ENDLOOP.
    READ TABLE LT_FIELDCAT INTO LS_FIELDCAT
      WITH KEY FIELDNAME = LS_EXCEL_HDR-FNAME.
    IF SY-SUBRC IS INITIAL.
*     Standard data using field catalog
      PERFORM STANDARD_VALUE_LVC
        USING LS_FIELDCAT
              I_HEADER
              <LF_DATA>
              ''
       CHANGING LS_EXCEL-VALUE.

*     Complete data using config
      PERFORM 9000_COMPLETE_CELL_DATA
        USING LS_EXCEL_HDR
              LS_FIELDCAT
        CHANGING LS_EXCEL-VALUE.
    ENDIF.
    CONDENSE: LS_EXCEL-ROW, LS_EXCEL-COLUMN, LS_EXCEL-VALUE.

    IF LS_EXCEL-VALUE IS NOT INITIAL OR LS_EXCEL-VALUE EQ ' '.
*     Check field name is page setup: Header, footer
      ASSIGN COMPONENT LS_EXCEL_HDR-FNAME OF STRUCTURE E_PAGESETUP
      TO <LF_PAGESETUP_F>.
      IF SY-SUBRC IS INITIAL.
        <LF_PAGESETUP_F> = LS_EXCEL-VALUE.
      ELSE.
        APPEND LS_EXCEL TO LS_EXCEL_EXP-EXDAT.
        IF I_LARGE_FILE IS NOT INITIAL.
          LS_EXCELN-VALUE = LS_EXCEL-VALUE.
          APPEND LS_EXCELN TO LS_EXCEL_EXP-EXDATN.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.
*  IF I_LARGE_FILE IS NOT INITIAL.
*    MOVE-CORRESPONDING LS_EXCEL_EXP-EXDAT TO LS_EXCEL_EXP-EXDATN.
*  ENDIF.
  APPEND LS_EXCEL_EXP TO T_EXCEL_EXP.





ENDFUNCTION.
