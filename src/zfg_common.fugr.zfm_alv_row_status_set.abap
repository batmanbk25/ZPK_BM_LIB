FUNCTION ZFM_ALV_ROW_STATUS_SET.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(I_MTYPE) TYPE  BAPI_MTYPE DEFAULT 'S'
*"     REFERENCE(I_DISABLE) TYPE  XMARK OPTIONAL
*"  EXPORTING
*"     REFERENCE(E_STATUS_ICON) TYPE  ZDD_STATUS_ICON
*"     REFERENCE(E_ROWCOLOR) TYPE  ANY
*"  CHANGING
*"     REFERENCE(C_ROW_DATA) TYPE  ANY
*"--------------------------------------------------------------------
DATA:
    LS_STYL           TYPE LVC_S_STYL.
  FIELD-SYMBOLS:
    <LF_FIELD>        TYPE ANY,
    <LFT_STYLEFNAME>  TYPE LVC_T_STYL.

* Set disable
  IF I_DISABLE IS SUPPLIED.
    ASSIGN COMPONENT GC_DISABLE_COLUMN OF STRUCTURE C_ROW_DATA
      TO <LF_FIELD>.
    IF SY-SUBRC IS INITIAL.
      <LF_FIELD>      = I_DISABLE.

      ASSIGN COMPONENT GC_STYLEFN_COLUMN OF STRUCTURE C_ROW_DATA
        TO <LFT_STYLEFNAME>.
      IF SY-SUBRC IS INITIAL.
        IF I_DISABLE = GC_XMARK.
*         Disable Selected
          LS_STYL-FIELDNAME       = GC_SEL_COLUMN.
          LS_STYL-STYLE           = CL_GUI_ALV_GRID=>MC_STYLE_DISABLED.
          MODIFY TABLE <LFT_STYLEFNAME> FROM LS_STYL.
          IF SY-SUBRC IS NOT INITIAL.
            INSERT LS_STYL INTO TABLE <LFT_STYLEFNAME>.
          ENDIF.
        ELSE.
*         Enable Selected
          LS_STYL-FIELDNAME       = GC_SEL_COLUMN.
          LS_STYL-STYLE           = CL_GUI_ALV_GRID=>MC_STYLE_ENABLED.
          MODIFY TABLE <LFT_STYLEFNAME> FROM LS_STYL.
          IF SY-SUBRC IS NOT INITIAL.
            INSERT LS_STYL INTO TABLE <LFT_STYLEFNAME>.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

* Set Status
  ASSIGN COMPONENT GC_MTYPE_COLUMN OF STRUCTURE C_ROW_DATA
    TO <LF_FIELD>.
  IF SY-SUBRC IS INITIAL.
    <LF_FIELD>      = I_MTYPE.
  ENDIF.

* Set Icon
  ASSIGN COMPONENT GC_ICON_COLUMN OF STRUCTURE C_ROW_DATA
    TO <LF_FIELD>.
  IF SY-SUBRC IS INITIAL.
    CALL FUNCTION 'ZFM_ALV_GENERATE_ICON_BY_MTYPE'
      EXPORTING
        I_MTYPE               = I_MTYPE
      IMPORTING
        E_STATUS_ICON         = <LF_FIELD>
        E_ROWCOLOR            = E_ROWCOLOR.
    E_STATUS_ICON = <LF_FIELD>.
  ENDIF.





ENDFUNCTION.
